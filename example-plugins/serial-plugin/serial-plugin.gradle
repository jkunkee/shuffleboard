plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
    maven {
        name = "WPILib"
        setUrl("https://frcmaven.wpi.edu/artifactory/release")
    }
}

dependencies {
    compileOnly project(":api")
    implementation group: 'com.fazecast', name: 'jSerialComm', version: '2.11.0'
}

// https://www.baeldung.com/gradle-fat-jar
tasks.register("augmentPluginJar", Jar) {
    baseName = baseName + '-withdeps'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    // Gotta filter out module_info.class or plugin loading breaks
    exclude("module-info.class")
    with jar
}

tasks.register("installPlugin", Copy) {
    from(tasks.named("augmentPluginJar"))
    into("${System.getProperty("user.home")}/Shuffleboard/plugins")
    description = "Builds the plugin JAR and installs it in the Shuffleboard plugins directory."
    group = "Shuffleboard Plugin"
}
